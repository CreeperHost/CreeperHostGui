evaluationDependsOnChildren() // gotta make sure the children happen first
import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.codehaus.groovy:groovy-json:2.4.1'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = "2.3.0.EA2"

allprojects {
    group= "net.creeperhost.minetogether"
    archivesBaseName = "minetogether"

    sourceCompatibility = targetCompatibility = "1.8"
    compileJava {
        sourceCompatibility = targetCompatibility = "1.8"
        options.encoding = "UTF-8"
    }
}

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly group: 'com.google.code.gson', name: 'gson', version: '2.2.4'
    compileOnly group: 'commons-io', name: 'commons-io', version: '2.4'
	  compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'
    compile 'org.kitteh.irc:client-lib:7.2.2'
    compile group: 'org.nanohttpd', name: 'nanohttpd', version: '2.3.1'
    compile 'com.github.scribejava:scribejava-apis:6.9.0'
}

repositories {
    jcenter()
    maven { url = "http://files.minecraftforge.net/maven" }
    mavenCentral()
}

task setupDecompWorkspace
task setupDevWorkspace
task setupCiWorkspace

ext.jarFile = zipTree(jar.archivePath)

task apiJar(type: Jar) {
    appendix = "api"
    from(project.ext.jarFile) {
       includes.addAll(["net/creeperhost/creeperhost/api/**"])
    }
}

subprojects {
    version = minecraft.version  + "-" + rootProject.version
	
    dependencies {
        compile rootProject
    }
    sourceSets.main.resources.srcDirs += rootProject.sourceSets.main.resources.srcDirs

    jar {
        duplicatesStrategy = 'exclude'
        from rootProject.sourceSets.main.output
    }
	
	shadowJar {
		duplicatesStrategy = 'exclude'
		from rootProject.sourceSets.main.output
	}

    sourceJar {
        duplicatesStrategy = 'exclude'
        from rootProject.sourceSets.main.allSource
    }

    ext.jarFile = zipTree(jar.archivePath)
	
    task apiJar(type: Jar, dependsOn: reobfJar) {
        appendix = "api"

        from(project.ext.jarFile) {
            includes.addAll(["net/creeperhost/creeperhost/api/**"])
        }
    }
    
    artifacts {
        archives sourceJar //apiJar
    }
    
    processResources {
        inputs.property "version", project.version
        inputs.property "mcversion", project.minecraft.version

        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            expand 'version':project.version, 'mcversion':project.minecraft.version
        }

        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }
    }
    
    // task deps
    rootProject.setupDecompWorkspace.dependsOn setupDecompWorkspace
    rootProject.setupDevWorkspace.dependsOn setupDevWorkspace
    rootProject.setupCiWorkspace.dependsOn setupCiWorkspace
    rootProject.build.dependsOn build
    rootProject.uploadArchives.dependsOn uploadArchives
	
    task transfer() {
        doLast {
            def out = new ByteArrayOutputStream()
            exec {
                commandLine "/usr/bin/curl",  "--silent", "--user", "$System.env.UPUSER", "--upload-file", jar.outputs.files.getFiles()[0], "https://upload.fdn.redstone.tech/minetogether/builds/" + jar.archiveName
                standardOutput = out
            }
            
            def json = new JsonSlurper().parseText(out.toString())
            println(json.result)
        }
    }
    
    transfer.shouldRunAfter rootProject.build
}
